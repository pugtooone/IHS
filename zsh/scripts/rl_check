#! /bin/zsh

# script for checking Ralph Lauren images
# version: 1.3.4
# fix rename_comp function by replacing ${cor_img_shot} with ${img_shot}
# created by Zeric Chan
#
# plan
# 1) [done] check image spec
# 2) [done] check filename with 1 character difference and rename them
# 3) [done] consider insert
# 4) [working] better report, might need sed

exec > /tmp/RL_check.log 2>&1

emulate -LR zsh
setopt xtrace
setopt extended_glob
setopt ksh_glob
setopt null_glob 

local PS4=$'+%1N:[%i]:%_> '

# array of correct file names
typeset -a RL_shotname=(
		    BagAngle BagFront BagBack BagDetail1 BagDetail2 BagInterior BagReversible BagSide
		    ShoeAngle ShoeBack ShoeOverhead ShoeProfile ShoeDetail1 ShoeDetail2
		    TABLETOP TT-DETAIL1 TT-DETAIL2 TT-DETAIL3
		    LAYDOWN LAYDOWNBACK SL-DETAIL1 SL-DETAIL2
		    BUST BUSTBACK
		   )

if [[ -e $1 ]]; then
	local PARENT=$(pwd)
	cd $1

	# parameter character sort function for wrong swap filename
	function charsort(){ print $(print ${(L)1} | grep -o . | sort | tr -d '\n') }

	function report_append(){
		if [[ $(grep -e "${img}" $RL_report) ]]; then
			return
		else
			print "${img}" >> $RL_report
		fi
	}

	function rename_img(){ mv "${img}" "${img_product}_${cor_shot_name}.tif" }

	function rename_comp(){ 
		if [[ ${cor_shot_name} == "" ]]; then
			mv "${img}" "${img_product}_${img_shot}_${img_comp}.tif" 
		else
			mv "${img}" "${img_product}_${cor_shot_name}_${img_comp}.tif"
		fi
	}

	rename -d ' ' *(.)
	local currentRL_image=($(print -l *.(tif|tiff|TIF|TIFF))) # files in current session
	local currentRL_product=($(print -l $currentRL_image | cut -d_ -f1 | uniq)) # products in current session
	mkdir rename
	local RL_rename=rename/
	print "RL_check Report\n\nno. of products: ${#currentRL_product}\nno. of images: ${#currentRL_image}\n\nimages renamed:" > rename/report && \
	       local RL_report=rename/report

	for img in $currentRL_image; do
		if [[ ${img} == *.(tiff|TIF|TIFF) ]]; then
			mv "${img}" "${img%.*}.tif"
			report_append
			local img="${img%.*}.tif"
		fi

		# check and modify image spec (imagemagick as dependency)
		local img_spec=($(identify -format '%r %h %x %e %z' "${img}[0]"))
		cor_img_spec=0
		[[ ${img_spec[2]} == sRGB ]] && cor_img_spec=$(( $cor_img_spec + 1 ))
		[[ ${img_spec[3]} == 4000 ]] && cor_img_spec=$(( $cor_img_spec + 1 ))
		[[ ${img_spec[4]} == 300 ]] && cor_img_spec=$(( $cor_img_spec + 1 ))
		[[ ${img_spec[5]} == tif ]] && cor_img_spec=$(( $cor_img_spec + 1 ))
		[[ ${img_spec[6]} == 8 ]] && cor_img_spec=$(( $cor_img_spec + 1 ))

		if [[ ${cor_img_spec} == 5 ]]; then
			:
		elif [[ ${cor_img_spec} == 4 ]]; then
			if [[ ${img_spec[2]} != sRGB ]]; then
				convert -colorspace sRGB "${img}[0]" "${img}"
				report_append
			elif [[ ${img_spec[3]} != 4000 ]]; then
				convert -resize x4000 "${img}[0]" "${img}"
				report_append
			elif [[ ${img_spec[4]} != 300 ]]; then
				convert -resample 300 "${img}[0]" "${img}"
				report_append
			elif [[ ${img_spec[5]} != tif ]]; then
				convert "${img}" "${img%.*}.tif"
				rm "${img}"
				local img="${img%.*}.tif"
				report_append
			elif [[ ${img_spec[6]} != 8 ]]; then
				convert -depth "${img}[0]" "${img}"
				report_append
			fi
		else
			convert -colorspace sRGB -resize x4000 -resample 300 -depth 8 "${img}" "${img%.*}.tif"
			rm "${img%.*}.(!(tif))" # remove $img that is not tif extension (ksh_glob required)
			local img="${img%.*}.tif"
			report_append
		fi

		# check and rename image filename
		local img_product=$(print $img | cut -d_ -f1)
		local img_shot=$(print ${img:s/.tif//} | cut -d_ -f2)
		local img_comp=$(print ${img:s/.tif//} | cut -d_ -f3)

		# modify RL_shotname to speedup loops
		if [[ ${(L)img_shot:0:3} == 'bag' ]]; then
			RL_shotname=($(print -l $RL_shotname | grep -i bag))
		elif [[ ${(L)img_shot:0:4} == 'shoe' ]]; then
			RL_shotname=($(print -l $RL_shotname | grep -i shoe))
		elif [[ ${(L)img_shot:0:5} == 'table' || ${(L)img_shot:0:2} == 'tt' ]]; then
			RL_shotname=($(print -l $RL_shotname | grep '^T'))
		fi

		# rename comp
		if [[ ${img_comp} == "" ]]; then
			:
		elif [[ ${img_comp} == COMP[1-9] || ${img_comp} == 'INSERT' ]]; then #COMP or INSERT
			:
		elif [[ ${(U)img_comp} == COMP[1-9] || ${(U)img_comp} == 'INSERT' ]]; then #comp or insert
			local img_comp=${(U)img_comp}
			rename_comp
			report_append
		elif [[ ${img_comp} == *[1-9] ]]; then #wrong spelling but end with digit
			local img_comp="COMP${img_comp[-1]}"
			rename_comp
			report_append
		elif [[ $(charsort ${(U)img_comp}) == 'EINRST' ]]; then
			local img_comp="INSERT"
			rename_comp
			report_append
		else 
			print "\nmanual rename required: ${img}" >> $RL_report
			mv ${img} $RL_rename
			continue
		fi

		# rename shot name
		if [[ $(print "${RL_shotname}" | grep -w "${img_shot}") ]]; then #total correct shot name without comp
			continue 
		elif [[ $(print "${(L)RL_shotname}" | grep -w "${(L)img_shot}") ]]; then #correct name but different capital
			for cor_shot_name in $RL_shotname; do
				if [[ ${(L)cor_shot_name} == ${(L)img_shot} && ${img_comp} == "" ]]; then
					rename_img
					report_append
					break
				elif [[ ${(L)cor_shot_name} == ${(L)img_shot} && ${img_comp} != "" ]]; then
					rename_comp
					report_append
					break
				fi
			done
			continue
		else
			for cor_shot_name in $RL_shotname; do
				# rename image with swapped character
				if [[ $(charsort ${(L)cor_shot_name}) == $(charsort ${(L)img_shot}) && ${img_comp} == "" ]]; then
					rename_img
					report_append
					continue 2
				elif [[ $(charsort ${(L)cor_shot_name}) == $(charsort ${(L)img_shot}) && ${img_comp} != "" ]]; then
					rename_comp
					report_append
					continue 2
				fi

				for (( i=1; i<(( ${#cor_shot_name} + 2 )); i++ )); do 
					# rename image with missing 1 character
					# remove 1 character from cor_shot_name and compare with img_shot
					if [[ ${(L)cor_shot_name/${cor_shot_name[$i]}/} == ${(L)img_shot} && ${img_comp} == "" ]]; then
						rename_img
						report_append
						continue 3
					elif [[ ${(L)cor_shot_name/${cor_shot_name[$i]}/} == ${(L)img_shot} && ${img_comp} != "" ]]; then
						rename_comp
						report_append
						continue 3
					fi
					# rename image with 1 extra character
					# remove 1 character from img_shot and compare with cor_shot_name
					if [[ $(charsort ${(L)cor_shot_name}) == $(charsort ${img_shot/${img_shot[$i]}/}) && ${img_comp} == "" ]]; then
						rename_img
						report_append
						continue 3
					elif [[ $(charsort ${(L)cor_shot_name}) == $(charsort "${img_shot/${img_shot[$i]}/}") && ${img_comp} != "" ]]; then
						rename_comp
						report_append
						continue 3
					fi
				done
			done
			print "$img" >> $RL_report
			mv ${img} $RL_rename
		fi
	done
	open $RL_report
	cd $PARENT
else
	print "ÔÅû Missing: Ralph Lauren session folder"
	exit 2
fi
